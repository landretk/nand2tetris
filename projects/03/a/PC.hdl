// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    //Generate incremented
    Inc16(in=out1, out=incIn);

    //Load whenever a control bit is set
    Or(a=load, b=inc, out=or1);
    Or(a=or1, b=reset, out=loadReg);

    Not(in=load, out=notLoad);
    And(a=notLoad, b=inc, out=loadSel1);
    Not(in=reset, out=notReset);
    And(a=notReset, b=loadSel1, out=loadSel);

    //Choose what to load
    Mux16(a=in, b=false, sel=reset, out=mux1);
    Mux16(a=mux1, b=incIn, sel=loadSel, out=regIn);

    Register(in=regIn, load=loadReg, out=out, out[0..15]=out1);
}
